buildscript {
	dependencies {
		classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
	}
}

plugins {
	id 'org.springframework.boot' version '2.5.6'
	id "com.bmuschko.docker-remote-api" version "6.7.0"
	id "io.freefair.lombok" version "5.3.0"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: "io.spring.dependency-management"

// This is used as the docker image prefix
group = 'com.gelerion.microservices'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

ext {
	springCloudVersion = '2020.0.3'
}

jar {
	baseName = 'licensing'
	version = '0.1.0'
}

repositories {
	mavenCentral()
	jcenter()
}

configurations {
	all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
}

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-web')
	implementation("org.springframework.boot:spring-boot-starter-data-jpa")

	implementation('org.springframework.boot:spring-boot-starter-actuator')
	implementation('org.springframework.boot:spring-boot-actuator-docs')

	implementation('org.springframework.boot:spring-boot-starter-log4j2')

	implementation('org.springframework.security.oauth:spring-security-oauth2')

	//cloud
	implementation('org.springframework.cloud:spring-cloud-starter-config')
	implementation('org.springframework.cloud:spring-cloud-config-client')

	implementation('org.springframework.cloud:spring-cloud-starter-eureka')
	implementation('org.springframework.cloud:spring-cloud-starter-feign')
	implementation('org.springframework.cloud:spring-cloud-starter-hystrix')

	implementation('org.springframework.cloud:spring-cloud-security')

	//custom
	implementation('com.h2database:h2')

	compileOnly('org.projectlombok:lombok')

	testImplementation('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}


import org.apache.tools.ant.filters.ReplaceTokens
processResources {
    with copySpec {
		from "$projectDir/docker/Dockerfile"
		into "../../docker/"
	}

	filter(ReplaceTokens, tokens: ['jarName': jar.archiveName])
}

task prepareDocker(type: Copy) {
	from("docker")
	from("${buildDir}/libs") {
		include '**/*.jar'
		rename "licensing-(.*)", 'licensing-server.jar'
	}

	into "${project.buildDir}/docker"
}

build.finalizedBy prepareDocker

//https://bmuschko.github.io/gradle-docker-plugin/
import com.bmuschko.gradle.docker.tasks.image.*
task buildImage(type: DockerBuildImage) {
	dependsOn build
	inputDir = file("${project.buildDir}/docker")
	images.add('licensing-server-image:latest')
}
