plugins {
	id 'org.springframework.boot' version '1.5.8.RELEASE'
	id "com.bmuschko.docker-remote-api" version "6.6.1"
	id "io.freefair.lombok" version "5.3.0"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

// This is used as the docker image prefix
group = 'com.gelerion.microservices.gateway'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

ext {
	springCloudVersion = 'Dalston.SR4'
}

jar {
	baseName = 'gateway'
	version = '0.1.0'
}

repositories {
	mavenCentral()
	jcenter()
}

configurations {
	all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
}

dependencies {
//	compile('org.springframework.boot:spring-boot-starter-web')

	compile('org.springframework.boot:spring-boot-starter-log4j2')

	//cloud
	compile('org.springframework.cloud:spring-cloud-starter-config')
	compile('org.springframework.cloud:spring-cloud-config-client')

	compile('org.springframework.cloud:spring-cloud-starter-eureka')
	compile('org.springframework.cloud:spring-cloud-starter-zuul')

	compile('com.google.guava:guava')
	compile('com.jason-goodwin:better-monads:0.4.0') {
		exclude module: 'scala-library'
	}

	compile('org.apache.httpcomponents:httpclient')

	compileOnly('org.projectlombok:lombok')

	testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

import org.apache.tools.ant.filters.ReplaceTokens
processResources {
    with copySpec {
		from "$projectDir/docker/Dockerfile"
		into "../../docker/"
	}

	filter(ReplaceTokens, tokens: ['jarName': jar.archiveName])
}

//noinspection GroovyAssignabilityCheck
task prepareDocker(type: Copy) {
	from("docker")
	from("${buildDir}/libs") {
		include "**/*.jar"
		rename "gateway-(.*)", 'gateway-server.jar'
	}

	into "${project.buildDir}/docker"
}

build.finalizedBy prepareDocker

//https://bmuschko.github.io/gradle-docker-plugin/
import com.bmuschko.gradle.docker.tasks.image.*
task buildImage(type: DockerBuildImage) {
	dependsOn build
	inputDir = file("${project.buildDir}/docker")
	images.add('gateway-server-image:latest')
}